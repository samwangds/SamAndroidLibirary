apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
version = "0.0.4"

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    resourcePrefix "samlib_"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 22
        versionCode 3
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'

    }
}



dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/jackson-core-asl-1.7.3.jar')
    compile files('libs/jackson-mapper-asl-1.7.3.jar')
    compile 'com.android.support:appcompat-v7:22.2.1'
}

def siteUrl = 'https://github.com/samwangds/SamAndroidLibrary'   // 项目的主页
def gitUrl = 'git@github.com:samwangds/SamAndroidLibrary.git'   // Git仓库的url
group = "com.sam.android.lib"            // Maven Group ID for the artifact，一般填你唯一的包名
description = 'An Android Library for quickly develop.'     //项目的描述 你可以多写一点
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
//        pom.artifactId ='base-library' //Bintray上你的项目的maven-metadata.xml文件的路径是gruopId+"/"+module名称。就只能让module名称和artifactId保持一致
        pom {
            project {
                packaging 'aar'
                // Add your description here
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'SamWang'        //填写的一些基本信息
                        name '山言两语'
                        email 'wds0405@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    options.encoding = "UTF-8"
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"
        name = "SamAndroidLibrary"    //发布到JCenter上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}
// gradlew install
// gradlew bintrayUpload